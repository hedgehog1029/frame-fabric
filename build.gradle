plugins {
    id 'java-library'
    id 'fabric-loom' version '0.11-SNAPSHOT'
    id 'maven-publish'
}

archivesBaseName = project.archives_base_name
version = project.mod_version
group = project.maven_group
description = "Frame support for Fabric."

java {
    toolchain {
        languageVersion.set(JavaLanguageVersion.of(17))
    }

    withSourcesJar()
}

repositories {
    maven {
        name = "offbeatwitch"
        url = "https://maven.offbeatwit.ch/repository/releases"
    }
}

dependencies {
    //to change the versions see the gradle.properties file
    minecraft "com.mojang:minecraft:${project.minecraft_version}"
    mappings "net.fabricmc:yarn:${project.yarn_mappings}:v2"

    modImplementation "net.fabricmc:fabric-loader:${project.loader_version}"
    modImplementation "net.fabricmc.fabric-api:fabric-api:${project.fabric_version}"

    modApi "io.github.hedgehog1029:frame:1.4.3"
    modApi "org.generalprogramming.fabulous:fabulous:1.0.0"

    include "io.github.hedgehog1029:frame:1.4.3"
}

processResources {
    inputs.property "version", project.version

    duplicatesStrategy = DuplicatesStrategy.INCLUDE

    from(sourceSets.main.resources.srcDirs) {
        include "fabric.mod.json"
        expand "version": project.version
    }
}

// ensure that the encoding is set to UTF-8, no matter what the system default is
// this fixes some edge cases with special characters not displaying correctly
// see http://yodaconditions.net/blog/fix-for-java-file-encoding-problems-with-gradle.html
tasks.withType(JavaCompile) {
    options.encoding = "UTF-8"
}

jar {
    from "LICENSE"
}

// configure the maven publication
publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java

            pom {
                name = "frame-fabric"
                description = project.description

                licenses {
                    license {
                        name = 'MIT License'
                        url = 'https://spdx.org/licenses/MIT.html'
                    }
                }

                developers {
                    developer {
                        id = 'obw'
                        name = 'offbeatwitch'
                        email = 'obw@generalprogramming.org'
                    }
                }

                scm {
                    connection = 'scm:git:git://github.com/hedgehog1029/frame-fabric.git'
                    url = 'https://github.com/hedgehog1029/frame-fabric'
                }
            }
        }
    }

    repositories {
        mavenLocal()
        maven {
            url = 'https://maven.offbeatwit.ch/repository/releases/'

            credentials(PasswordCredentials) {
                username = project.property('archiva.user')
                password = project.property('archiva.password')
            }
        }
    }
}
